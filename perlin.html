<?DOCTYPE html?>
<html>
<head>
    <script type="text/x-shader" id="vertex-shader-src">
    precision mediump float;

    attribute vec2 vertexPosition;
    varying vec2 fragmentPosition;

    void main() {
        // convert the 2d position to a 4d vector
        gl_Position = vec4(vertexPosition, 0, 1);
        // interpolate coords over the whole screen
        fragmentPosition = vertexPosition;
    }
    </script>
    <script type="text/x-shader" id="fragment-shader-src">
    precision mediump float;

    uniform sampler2D noiseTexture;
    varying vec2 fragmentPosition;

    float noise2d(vec2 pos, float scale) {
        // pos is from fragmentPosition, which ranges from -1 to 1
        // move it to texture pixel space (0 to 1023, but it can wrap)
        vec2 scaled = scale * pos;
        vec2 topLeft = floor(scaled);
        vec2 topRight = topLeft + vec2(1.0, 0.0);
        vec2 bottomLeft = topLeft + vec2(0.0, 1.0);
        vec2 bottomRight = topLeft + vec2(1.0, 1.0);

        vec2 normTL = texture2D(noiseTexture, (2.0 * topLeft + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normTR = texture2D(noiseTexture, (2.0 * topRight + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normBL = texture2D(noiseTexture, (2.0 * bottomLeft + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normBR = texture2D(noiseTexture, (2.0 * bottomRight + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;

        vec2 offset = scaled - topLeft;
        float yScale = 3.0 * offset.y * offset.y - 2.0 * offset.y * offset.y * offset.y;
        float left = mix(dot(normTL, scaled - topLeft), dot(normBL, scaled - bottomLeft), yScale);
        float right = mix(dot(normTR, scaled - topRight), dot(normBR, scaled - bottomRight), yScale);

        float xScale = 3.0 * offset.x * offset.x - 2.0 * offset.x * offset.x * offset.x;
        return mix(left, right, xScale);
    }

    // iters should be [1,5] or so
    // persist should be [0,1]
    float octaveNoise2d(vec2 pos, float scale, int iters, float persist) {
        float result = 0.0;
        float factor = 1.0;
        for (int i = 0; i < 64; i++) {
            if (i >= iters) {
                break;
            }
            result += factor * noise2d(pos, scale);
            scale *= 2.0;
            factor *= persist;
        }
        return result;
    }

    #define SCALE 10.

    void main() {
        vec2 pos = floor(fragmentPosition * 128.) / 128.;
        float limit = octaveNoise2d(vec2(pos.x / 6., 1.), SCALE, 5, 0.5);
        if (pos.y + 0.5 < limit / 4.) {
            float result = (octaveNoise2d(pos + 1.0, SCALE / 2., 4, 0.65) + 1.) / 2.0;
            int idx = int(floor(result * 8.));
            if (idx == 7) {
                gl_FragColor = vec4(156., 168., 100., 255.) / 255.;
            } else if (idx == 6) {
                gl_FragColor = vec4(104., 112., 52., 255.) / 255.;
            } else if (idx == 0) {
                gl_FragColor = vec4(160., 132., 68., 255.) / 255.;
            } else if (idx == 1) {
                gl_FragColor = vec4(100., 72., 24., 255.) / 255.;
            } else {
                float grey = 65. + 45. * float(idx - 2);
                gl_FragColor = vec4(grey, grey, grey, 255.) / 255.;
            }
        } else {
            gl_FragColor = vec4(124., 180., 212., 255.) / 255.;
        }
    }

    </script>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
    <script src="perlin.js"></script>
</head>
<body>
    <canvas width="1024" height="720"></canvas>
</body>
</html>
