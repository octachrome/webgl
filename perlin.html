<?DOCTYPE html?>
<html>
<head>
    <script type="text/x-shader" id="vertex-shader-src">
    precision mediump float;

    attribute vec2 vertexPosition;
    varying vec2 fragmentPosition;

    void main() {
        // convert the 2d position to a 4d vector
        gl_Position = vec4(vertexPosition, 0, 1);
        // interpolate coords over the whole screen
        fragmentPosition = vertexPosition;
        // bubbles are 4px across
        gl_PointSize = 4.;
    }
    </script>
    <script type="text/x-shader" id="fragment-shader-src">
    precision mediump float;

    uniform int shaderMode;
    uniform float offset;
    uniform sampler2D noiseTexture;
    varying vec2 fragmentPosition;

    float noise2d(vec2 pos, float scale) {
        // pos is from fragmentPosition, which ranges from -1 to 1
        // move it to texture pixel space (0 to 1023, but it can wrap)
        vec2 scaled = scale * pos;
        vec2 topLeft = floor(scaled);
        vec2 topRight = topLeft + vec2(1.0, 0.0);
        vec2 bottomLeft = topLeft + vec2(0.0, 1.0);
        vec2 bottomRight = topLeft + vec2(1.0, 1.0);

        vec2 normTL = texture2D(noiseTexture, (2.0 * topLeft + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normTR = texture2D(noiseTexture, (2.0 * topRight + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normBL = texture2D(noiseTexture, (2.0 * bottomLeft + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;
        vec2 normBR = texture2D(noiseTexture, (2.0 * bottomRight + vec2(1.0, 1.0)) / 2048.0).xy * 2.0 - 1.0;

        vec2 offset = scaled - topLeft;
        float yScale = 3.0 * offset.y * offset.y - 2.0 * offset.y * offset.y * offset.y;
        float left = mix(dot(normTL, scaled - topLeft), dot(normBL, scaled - bottomLeft), yScale);
        float right = mix(dot(normTR, scaled - topRight), dot(normBR, scaled - bottomRight), yScale);

        float xScale = 3.0 * offset.x * offset.x - 2.0 * offset.x * offset.x * offset.x;
        return mix(left, right, xScale);
    }

    // iters should be [1,5] or so
    // persist should be [0,1]
    float octaveNoise2d(vec2 pos, float scale, int iters, float persist) {
        float result = 0.0;
        float factor = 1.0;
        for (int i = 0; i < 64; i++) {
            if (i >= iters) {
                break;
            }
            result += factor * noise2d(pos, scale);
            scale *= 2.0;
            factor *= persist;
        }
        return result;
    }

    #define SCALE 10.

    void background() {
        // discretise the screen coords
        vec2 pos = floor((fragmentPosition + vec2(offset, 0.)) * 128.) / 128.;
        // height of sea floor
        float limit = octaveNoise2d(vec2(pos.x / 8., 1.), SCALE, 6, 0.6);
        if (pos.y + 0.5 < limit / 4.) {
            // lightness
            float result = 1. - abs(octaveNoise2d((pos + 1.0) * vec2(1., .5), SCALE / 1.5, 6, 0.6));
            float grey;
            if (result > 0.995) {
                grey = .9;
            } else if (result > 0.97) {
                grey = .8;
            } else if (result > 0.85) {
                grey = .7;
            } else {
                grey = floor(result * 5.) / 5. * .8;
            }
            // rock or weed
            float type = abs(octaveNoise2d((pos + 1.) * .1, SCALE, 8, 0.7));
            if (type < 0.05) {
                int idx = int(grey * 5.);
                if (fract(type * 20.) < 0.5) {
                    // greens
                    if (idx == 0) {
                        gl_FragColor = vec4(44., 48., 0., 255.) / 255.;
                    } else if (idx == 1) {
                        gl_FragColor = vec4(76., 80., 28., 255.) / 255.;
                    } else if (idx == 2) {
                        gl_FragColor = vec4(132., 140., 76., 255.) / 255.;
                    } else if (idx == 3) {
                        gl_FragColor = vec4(156., 168., 100., 255.) / 255.;
                    } else if (idx == 4) {
                        gl_FragColor = vec4(180., 192., 120., 255.) / 255.;
                    }
                } else {
                    // browns
                    if (idx == 0) {
                        gl_FragColor = vec4(27., 68., 40., 255.) / 255.;
                    } else if (idx == 1) {
                        gl_FragColor = vec4(100., 72., 24., 255.) / 255.;
                    } else if (idx == 2) {
                        gl_FragColor = vec4(132., 104., 48., 255.) / 255.;
                    } else if (idx == 3) {
                        gl_FragColor = vec4(160., 132., 68., 255.) / 255.;
                    } else if (idx == 4) {
                        gl_FragColor = vec4(184., 156., 88., 255.) / 255.;
                    }
                }
            } else {
                gl_FragColor = vec4(grey, grey, grey, 1.);
            }
        } else {
            // sea
            gl_FragColor = vec4(124., 180., 212., 255.) / 255.;
        }
    }

    void bubbles() {
        gl_FragColor = vec4(1., 1., 1., 1.);
    }

    void main() {
        if (shaderMode == 0) {
            background();
        } else {
            bubbles();
        }
    }

    </script>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
    <script src="perlin.js"></script>
</head>
<body>
    <canvas width="1024" height="720"></canvas>
</body>
</html>
